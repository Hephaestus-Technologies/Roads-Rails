def rr_version = version = "0.0.1"
def mc_major_version     = "1.12"
def mc_version_full      = "${mc_major_version}.2".toString()
def forge_version        = "14.23.5.2768"
def ccl_version          = "3.2.2"
def fml_version          = "2.6.0"
def jei_version          = "4.8.0"

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'scala'

repositories {
    maven {
        name "chickenbones"
        url "http://chickenbones.net/maven/"
    }

    maven {
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
}

group = "com.hephaestus_technologies"
archivesBaseName = "RoadsAndRails"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version_full}-${forge_version}"
    runDir = "run"
    mappings = "snapshot_20171003"
}

dependencies {
    compile "codechicken:CodeChickenLib:${mc_version_full}-${ccl_version}.+:deobf"
    compile "codechicken:ForgeMultipart:${mc_version_full}-${fml_version}.+:deobf"
    compile "codechicken:ChickenASM:${mc_major_version}-1.0.2.7"
    deobfCompile "mezz.jei:jei_${mc_version_full}:${jei_version}.+:api"
    runtime "mezz.jei:jei_${mc_version_full}:${jei_version}.+"
}

sourceSets {
    main {
        def root = project.projectDir
        scala {
            srcDir new File(root, "src")
        }
        resources {
            srcDir new File(root, "resources")
        }
    }
}

processResources
{
    inputs.property "version", rr_version
    inputs.property "mcversion", mc_major_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand  'mod_version'  : rr_version,
                'mc_version'   : mc_major_version,
                'forge_version': forge_version,
                'ccl_version'  : ccl_version,
                'fmp_version'  : fml_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
